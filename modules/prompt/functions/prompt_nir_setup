#
# A fork of the Sorin theme. Various tweaks:
# - colors work better with solarized theme
# - better display of hashed (named) directories
# - slight changes to how git information is presented
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Tweaks by:
#   Nir Friedman <quicknir@gmail.com>

# 16 Solarized Terminal Colors
# -- ---------------
#  0 base02 (light black)
#  1 red
#  2 green
#  3 yellow
#  4 blue
#  5 magenta
#  6 cyan
#  7 base2 (dark white)
#  8 base03 (dark black)
#  9 orange
# 10 base01 (dark dark grey)
# 11 base00 (dark grey)
# 12 base0 (light grey)
# 13 violet
# 14 base1 (light light grey)
# 15 base3 (bright white)

# Load dependencies.
pmodload 'helper'
source ${ZDOTDIR:-$HOME}/.zprezto/modules/prompt/functions/prompt_sorin_setup

# Looks like zsh allows us to redefine a function.
# In this case, we redefine the calculation of the pwd string
# so that hashed directories are respected
function prompt-pwd {
  # Expand pwd taking into account hashed directories
  local pwd_esc="%~"
  local pwd_expand=${(%)pwd_esc}

  # Split on /
  local arr
  arr=("${(s:/:)pwd_expand}")
  local start

  # if first token starts with ~, don't apply first letter transformation to it
  if [[ ${a[1]:0:1} == "~" ]]; then
    start=1
  else
    start=2
  fi

  for (( i=$start; i < $#arr; i++ )); do
    if [[ ${arr[i]:0:1} == "." ]]; then
      arr[i]=${arr[i]:0:2}
    else
      arr[i]=${arr[i]:0:1}
    fi
  done

  print "${(j:/:)arr}"
}

# Different setup function, mostly to specify different git info, change some
# colors of the prompts.
function prompt_nir_setup {
  prompt_sorin_setup "$@"

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %F{red}❯%F{yellow}❯%F{green}❯%f'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %F{green}❮%F{yellow}❮%F{red}❮%f'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%F{yellow}%s%f'
  zstyle ':prezto:module:git:info:added' format ' %F{yellow}%a✚%f'
  zstyle ':prezto:module:git:info:ahead' format ' %F{green}%A⬆%f'
  zstyle ':prezto:module:git:info:behind' format ' %F{red}%B⬇%f'
  zstyle ':prezto:module:git:info:branch' format '%F{9}%b%f'
  zstyle ':prezto:module:git:info:deleted' format ' %F{red}%d✖%f'
  zstyle ':prezto:module:git:info:modified' format ' %F{blue}%m✱%f'
  zstyle ':prezto:module:git:info:position' format ':%F{red}%p%f'
  zstyle ':prezto:module:git:info:renamed' format ' %F{magenta}%r➜%f'
  zstyle ':prezto:module:git:info:stashed' format ' %F{cyan}✭%f'
  zstyle ':prezto:module:git:info:unmerged' format ' %F{yellow}═%f'
  zstyle ':prezto:module:git:info:untracked' format ' %F{14}%u◼%f'
}

prompt_nir_setup "$@"
